plugins {
    id 'io.ehdev.gradle.dependency-resolve' version 'LATEST' apply false
    id 'io.ehdev.gradle.dependency-script' version 'LATEST' apply false
    id 'io.ehdev.gradle.build-dir' version 'LATEST' apply false
    id 'io.ehdev.gradle.klint' version 'LATEST' apply false
    id "org.flywaydb.flyway" version "5.1.4" apply false
    id 'org.jetbrains.kotlin.jvm' version '1.2.51' apply false
    id "org.jetbrains.kotlin.kapt" version "1.2.51" apply false
}

def versionProperties = new Properties()
versionProperties.load(new FileInputStream("$rootDir/version.properties"))
ext['version'] = versionProperties.getProperty("version")

apply from: "gradle/database.gradle"

allprojects {
    group 'io.ehdev'
    version rootProject.ext.version

    apply plugin: 'io.ehdev.gradle.build-dir'
    apply plugin: 'io.ehdev.gradle.dependency-resolve'
    apply plugin: 'io.ehdev.gradle.dependency-script'
    apply plugin: 'java'

    repositories {
        jcenter()
        mavenCentral()
    }

    sourceCompatibility = '1.10'

    dependencies {
        modules {
            module("org.hibernate:hibernate-validator") {
                replacedBy("org.hibernate.validator:hibernate-validator",
                        "org.hibernate is now part of org.hibernate.validator")
            }
        }

        testCompile("org.junit.jupiter:junit-jupiter-api:5.2.0")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:5.2.0")
    }

    plugins.withType(org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapper) {
        plugins.apply('io.ehdev.gradle.klint')
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }
}
